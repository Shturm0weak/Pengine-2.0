#version 450

layout(set = 1, binding = 0) uniform sampler2D albedoTexture;
layout(set = 1, binding = 1) uniform sampler2D normalTexture;
layout(set = 1, binding = 2) uniform sampler2D shadingTexture;
layout(set = 1, binding = 3) uniform sampler2D depthTexture;
layout(set = 1, binding = 4) uniform sampler2D ssaoTexture;
layout(set = 1, binding = 5) uniform sampler2DArray CSMTexture;

#include "Shaders/Includes/IsBrightPixel.h"
#include "Shaders/Includes/PointLight.h"
#include "Shaders/Includes/DirectionalLight.h"
#include "Shaders/Includes/CSM.h"
#include "Shaders/Includes/Camera.h"

layout(set = 0, binding = 0) uniform GlobalBuffer
{
	Camera camera;
};

layout(set = 2, binding = 0) uniform Lights
{
	PointLight pointLights[32];
	int pointLightsCount;

	DirectionalLight directionalLight;
	int hasDirectionalLight;

	float brightnessThreshold;

	CSM csm;
};

layout(set = 3, binding = 0, r11f_g11f_b10f) uniform writeonly image2D outColor;
layout(set = 3, binding = 1, rgba16f) uniform image2D outEmissive;

layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;
void main()
{
    ivec2 pixelCoord = ivec2(gl_GlobalInvocationID.xy);
    vec2 uv = (vec2(pixelCoord) + vec2(0.5f)) / camera.viewportSize;
	vec2 screenPosition = uv * 2.0f - 1.0f;
	uv.y = 1.0f - uv.y;

	vec2 viewRay;
	viewRay.x = -screenPosition.x * camera.aspectRatio * camera.tanHalfFOV;
    viewRay.y = -screenPosition.y * camera.tanHalfFOV;

	vec3 albedoColor = texture(albedoTexture, uv).xyz;
	vec3 shading = texture(shadingTexture, uv).xyz;

	vec4 normalViewSpace = texture(normalTexture, uv);
	vec3 ssao = texture(ssaoTexture, uv).xyz;

	vec3 result = vec3(0.0f);

	if (normalViewSpace.a <= 0.0f)
	{
		result = albedoColor;
	}
	else
	{
		vec3 positionViewSpace = CalculatePositionFromDepth(
			texture(depthTexture, uv).x,
			camera.projectionMat4,
			viewRay);

		if (hasDirectionalLight == 1)
		{
			vec3 shadow = vec3(0.0f);
			if (csm.isEnabled == 1)
			{
				vec3 worldSpacePosition = (camera.inverseViewMat4 * vec4(positionViewSpace, 1.0f)).xyz;

				shadow = CalculateCSM(
					CSMTexture,
					csm,
					abs(positionViewSpace.z),
					worldSpacePosition,
					normalViewSpace.xyz,
					directionalLight.direction);
			}

			vec3 basicReflectivity = mix(vec3(0.05), albedoColor, shading.x);
			vec3 viewDirection = normalize(-positionViewSpace);
			result += CalculateDirectionalLight(
				directionalLight,
				viewDirection,
				basicReflectivity,
				normalViewSpace.xyz,
				albedoColor,
				shading.x,
				shading.y,
				shading.z,
				shadow);
		}

		for (int i = 0; i < pointLightsCount; i++)
		{
			result += CalculatePointLight(pointLights[i], positionViewSpace, normalViewSpace.xyz) * albedoColor;
		}
	}

	pixelCoord.y = int(camera.viewportSize.y) - pixelCoord.y;

    vec4 emissive = vec4(IsBrightPixel(result, brightnessThreshold), 1.0f);
    emissive += imageLoad(outEmissive, pixelCoord);
    imageStore(outEmissive, pixelCoord, emissive);
    
    imageStore(outColor, pixelCoord, vec4(result * ssao, 1.0f));
}
