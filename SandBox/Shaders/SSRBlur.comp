#version 450

layout(set = 0, binding = 0, rgba8) uniform writeonly image2D outColor;
layout(set = 0, binding = 1) uniform sampler2D sourceTexture;
layout(set = 0, binding = 2) uniform sampler2D normalTexture;
layout(set = 0, binding = 3) uniform sampler2D shadingTexture;
layout(set = 0, binding = 4) uniform sampler2D depthTexture;

layout(set = 0, binding = 5) uniform SSRBlur
{
    int blurRange;
    int blurOffset;
    int blur;
};

layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;
void main()
{
	ivec2 pixelCoord = ivec2(gl_GlobalInvocationID.xy);
	vec2 sourceTextureSize = vec2(textureSize(sourceTexture, 0));
    vec2 texelSize = 1.0f / sourceTextureSize;
	vec2 outColorSize = vec2(imageSize(outColor));
    vec2 uv = (vec2(pixelCoord) + vec2(0.5f)) / outColorSize;

    if (blur == 0)
    {
        imageStore(outColor, pixelCoord, texture(sourceTexture, uv));
    }
    else if (blur == 1)
    {
        int n = 0;
        vec4 result = vec4(0.0f);
        for (int x = -blurRange; x <= blurRange; x++)
        {
            for (int y = -blurRange; y <= blurRange; y++)
            {
                vec2 offset = vec2(float(x) * blurOffset, float(y) * blurOffset) * texelSize;
                result += texture(sourceTexture, uv + offset);
                n++;
            }
        }

        imageStore(outColor, pixelCoord, vec4(result / float(n)));
    }
    else if (blur == 2)
    {
        float roughness = texture(shadingTexture, uv).y;
        int kernelRadius = int(mix(1.0f, 4.0f, roughness));
        
        vec4 sum = vec4(0.0f);
        float totalWeight = 0.0f;
        
        vec3 normal = texture(normalTexture, uv).rgb;
        float depth = texture(depthTexture, uv).r;

        for (int x = -kernelRadius; x <= kernelRadius; ++x)
        {
            for (int y = -kernelRadius; y <= kernelRadius; ++y)
            {
                vec2 sampleUV = uv + vec2(x, y) * texelSize;
                
                if (any(lessThan(sampleUV, vec2(0.0f))) || any(greaterThan(sampleUV, vec2(1.0f)))) 
                    continue;
                
                vec3 sampleNormal = texture(normalTexture, sampleUV).rgb;
                float sampleDepth = texture(depthTexture, sampleUV).r;
                
                float spatialWeight = exp(-0.5f * dot(vec2(x, y), vec2(x, y)) / (roughness * 4.0f));
                
                float normalWeight = pow(max(0.0f, dot(normal, sampleNormal)), 8.0f);
                float depthWeight = exp(-abs(depth - sampleDepth) * 100.0f);
                
                float bilateralWeight = spatialWeight * normalWeight * depthWeight;
                
                sum += texture(sourceTexture, sampleUV) * bilateralWeight;
                totalWeight += bilateralWeight;
            }
        }

        imageStore(outColor, pixelCoord, vec4(sum / max(totalWeight, 0.001f)));
    }
}
