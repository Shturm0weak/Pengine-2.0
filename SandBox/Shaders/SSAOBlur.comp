#version 450

layout(set = 0, binding = 0, r8) uniform writeonly image2D outColor;
layout(set = 0, binding = 1) uniform sampler2D sourceTexture;

float boxFilter4Tap(sampler2D tex, vec2 uv)
{
    vec4 samples = textureGather(tex, uv, 0);
    
    return (samples.x + samples.y + samples.z + samples.w) * 0.25f;
}

layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;
void main()
{
	ivec2 pixelCoord = ivec2(gl_GlobalInvocationID.xy);
	vec2 sourceTextureSize = vec2(textureSize(sourceTexture, 0));
	vec2 outColorSize = vec2(imageSize(outColor));
    vec2 uv = (vec2(pixelCoord) + vec2(0.5f)) / outColorSize;

    vec2 texelSize = 1.0f / sourceTextureSize;
    vec2 alignedUV = uv - texelSize * 0.5f;
    
    float block1 = boxFilter4Tap(sourceTexture, alignedUV);
    float block2 = boxFilter4Tap(sourceTexture, alignedUV + vec2(2.0f * texelSize.x, 0.0f));
    float block3 = boxFilter4Tap(sourceTexture, alignedUV + vec2(0.0f, 2.0f * texelSize.y));
    float block4 = boxFilter4Tap(sourceTexture, alignedUV + vec2(2.0f * texelSize.x, 2.0f * texelSize.y));
    
    float result = (block1 + block2 + block3 + block4) * 0.25f;

    imageStore(outColor, pixelCoord, vec4(result));
}
