#version 450

layout(location = 0) in vec3 positionA;
layout(location = 1) in vec2 uvA;
layout(location = 2) in uint colorA;
layout(location = 3) in mat4 transformA;
layout(location = 7) in int lightIndexA;
layout(location = 8) in int faceIndexA;

layout(location = 0) out vec2 uv;
layout(location = 1) out vec4 positionWorldSpace;
layout(location = 2) flat out vec3 lightPositionWorldSpace;
layout(location = 3) flat out float radius;

#include "Shaders/Includes/Camera.h"
#include "Shaders/Includes/DirectionalLight.h"
#include "Shaders/Includes/PointLight.h"
#include "Shaders/Includes/CSM.h"
#include "Shaders/Includes/SSS.h"

layout(set = 1, binding = 0) uniform Lights
{
	PointLight pointLights[32];
	int pointLightsCount;

	DirectionalLight directionalLight;
	int hasDirectionalLight;

	float brightnessThreshold;

	CSM csm;

	PointLightShadows pointLightShadows;
    
    SSS sss;
};

layout(set = 2, binding = 0) uniform GlobalBuffer
{
	Camera camera;
};

void main()
{
    vec4 windParams = unpackUnorm4x8(colorA);
	float stiffness = windParams.r;
    float oscillation = windParams.g;

	float windWave = sin(camera.time * camera.wind.frequency + float(gl_VertexIndex) * oscillation);

	float windInfluence = (1.0f - stiffness) * camera.wind.strength;
	vec3 windDisplacement = camera.wind.direction * windWave * windInfluence;

    positionWorldSpace = transformA * vec4(windDisplacement + positionA, 1.0f);
	gl_Position = pointLights[lightIndexA].pointLightFaceInfos[faceIndexA].viewProjectionMat4 * positionWorldSpace;
    lightPositionWorldSpace = pointLights[lightIndexA].positionWorldSpace;
    radius = pointLights[lightIndexA].radius;
	uv = uvA;
}
